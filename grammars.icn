#  John Goettsche
#  under the direction of Clinton Jeffery
#  University of Idaho

#grammars.icn
package rawGrammar
import production

procedure createNonTerminals(fileName, args, rawGrammar)  
#  from the rawGrammar it creates the NonTerminals and
#  procedes the createTerminals procedure

   #create nonTerminals
   local 
      nonTerminal, 
      nonTerminalWords, 
      nonTerminalsSet, 
      nonTerminalCheckSet,
      outfile
      
   nonTerminalWords := []
   nonTerminalCheckSet := set()
   every nonTerminal := getNonTerminals(rawGrammar) do 
      if not member(nonTerminalCheckSet, nonTerminal) then {
         put(nonTerminalWords, nonTerminal)
         insert(nonTerminalCheckSet, nonTerminal)
      }
   #every write(!nonTerminalCheckSet)
   nonTerminalsList := []
   every put(nonTerminalsList, makeNonTerminalsSet(nonTerminalWords))

   if *args > 0 then if member(args, "n") then {
      outfile := &output
      outfileName := fileName || "-nonTerminals.txt"
      if member(args, "o") then outfile := open(outfileName, "c")
      write("\nNon Terminals\n") 
      if outfile ~=== &output then every write(outfile, !nonTerminalWords)
      every write("\t" || !nonTerminalWords)
      write("\n(press ENTER to continue)\n")
      getch()
   }  
   createTerminals(fileName, args, rawGrammar, nonTerminalCheckSet, nonTerminalsList)
end

procedure createTerminals(fileName, args, rawGrammar, nonTerminalCheckSet, nonTerminalsSet)
#  from the rawGrammar, nonTerminalCheckSet and nonTerminalSet it
#  produces the terminals and procedes to the creaeGrammar procedure

   #create terminals
   local 
      terminalsList, 
      terminalsSet, 
      terminalCheckSet, 
      terminal,
      outfile
      
   terminalsList := []
   terminalCheckSet := set()
   every terminal := getTerminals(rawGrammar, nonTerminalCheckSet) do 
      if not member(terminalCheckSet, terminal) then { 
         put(terminalsList, terminal)
         insert(terminalCheckSet, terminal)
      }
   terminalsSet := []
   every put(terminalsSet, makeTerminalsSet(terminalsList))

   if *args > 0 then if member(args, "t") then {
      outfile := &output
      outfileName := fileName || "-terminals.txt"
      if member(args, "o") then outfile := open(outfileName, "c")
      write("\nTerminals\n") 
      if outfile ~=== &output then every write(outfile, !terminalsList)
      every write("\t" || !terminalsList)
      write("\n(press ENTER to continue)\n")
      getch()
   }  
   #write(production)
   production::createProductionRules(
         fileName,
         args,
         rawGrammar, 
         nonTerminalCheckSet, 
         nonTerminalsSet, 
         terminalCheckSet, 
         terminalsSet)
end

procedure getNonTerminals(wordList)
#  finds and suspends all the NonTerminals in the list (rawGrammar)

   local x
   x := 1
   while wordList[x] do {
      if wordList[x] ~== "|" then suspend wordList[x]
      x +:= 1
      while wordList[x] ~== ";" do x := x + 1
      x +:= 1
   } 
end

procedure makeNonTerminalsSet(nonTerminalWords)
#  creates and suspends each NonTerminal in the nonTerminalWords
# in the order they appear in the grammar

   local x, result, thisNonTerminal
   every result := !nonTerminalWords do {
      thisNonTerminal := NonTerminal()
      thisNonTerminal.setResult(result)
      suspend thisNonTerminal
   }
end

procedure makeTerminalsSet(terminalsList)
# creates and susends each Terminal in the terminalsList

   local result, thisTerminal
   every result := !terminalsList do {
      thisTerminal := Terminal(0)
      thisTerminal.setResult(result)
      suspend thisTerminal
   }
end

procedure getTerminals(wordList, nonTerminalsList)
#  finds all the Terminals in the list and creates and suspends
#  each terminal

   local x
   x := 1
   #write(list[x])
   #every write(!nonTerminalsList)
   while wordList[x] do {
      if not member(nonTerminalsList, wordList[x]) then {
         if not (wordList[x] == (":"|";"|"|")) then
	    suspend wordList[x]
      }
      x := x + 1
   }
end

class Terminal(type, result)
#  used to hold a single terminal

   method setResult(newResult)
      result := newResult
   end

   method getType()
      return type
   end

   method getResult()
      return result
   end   

initially (thisType)
   type := thisType
end

class NonTerminal(type, result, productionRules)
#  used to hold a single non terminal

   method setResult(newResult)
      result := newResult
   end

   method addProductionRule(newProductionRule)
      put(productionRules, newProductionRule)
   end

   method getType()
      return type
   end

   method getResult()
      return result
   end

   method findRule(matchRule)
      local productionRule
      every productionRule := !productionRules do
         if productionRule == matchRule then return productionRule
   end
      
   method getProductionRule(r)
      return productionRules[r]
   end

   method getProductionRules()
      local productionRule
      every productionRule := !productionRules do suspend productionRule
   end

   method getTxt()      
      local productionRule, thisString
      every productionRule := !productionRules do {
         thisString := result || " <- " || productionRule.getTxt()
         suspend thisString
      }
   end
   
initially
   type := 1
   productionRules := []
end
